package "novex/astro/tree/nodes"

import "nova/regex/Pattern"
import "nova/datastruct/list/ImmutableArray"

import "novex/astro/SyntaxErrorException"
import "novex/astro/InvalidParseException"
import "novex/astro/SyntaxMessage"
import "novex/astro/util/Location"
import "novex/astro/tree/nodes/generics/GenericParameter"
import "novex/astro/tree/nodes/generics/GenericCompatible"
import "novex/astro/tree/nodes/variables/InstanceDeclaration"
import "novex/astro/tree/nodes/variables/VariableDeclaration"
import "novex/astro/tree/nodes/variables/FieldDeclaration"
import "novex/astro/tree/nodes/variables/ArrayBracketOverload"
import "novex/astro/tree/nodes/functions/FunctionDeclaration"
import "novex/astro/tree/nodes/functions/Constructor"
import "novex/astro/tree/nodes/functions/BodyFunction"

import "novex/astro/util/CompilerStringFunctions"

/// @InstanceDeclaration extension that represents 
///
/// author: Braden Steffaniak
/// since: 
/// version: 
class ClassDeclaration extends InstanceDeclaration implements Abstractable, GenericCompatible {
    visible String identifier => "class"
    
    visible ClassDeclaration parentClass => this
    
    visible String location =>
        parentFile.packageDeclaration.location ?
            parentFile.packageDeclaration.location + "/" + name :
            name
    
    public var Type extendedClass
    public var Type[] implementedInterfaces = new Type[0]
    
    public var FieldDeclaration[] fields = new FieldDeclaration[]
    public var FunctionDeclaration[] functions = new FunctionDeclaration[]
    
    public immutable Constructor[] constructors => functions.filter({ _.class.isOfType(Constructor) })
    
    visible var ArrayBracketOverload arrayBracketOverload
    
    visible Bool isPrivate => visibility.count > 0 && super.isPrivate
    visible Bool isPublic => visibility.count == 0 || super.isPublic
    
    /// Create a new ClassDeclaration node.
    /// 
    /// #exampleInputs
    /// 
    /// parent:   The parent of the ClassDeclaration node.
    /// location: The location of the input text in the source file.
    public construct(Node parent = null, Location location = Location.INVALID) {
        super(parent, location)
        
        genericParameters = new GenericParameter[0]
    }
    
    public addChild(Node child) {
        if (addGlobalNode(child)) {
            
        } else if (child.class.isOfType(FieldDeclaration)) {
            fields.add(child)
        } else if (child.class.isOfType(FunctionDeclaration)) {
            functions.add(child)
        } else if (child.class.isOfType(ArrayBracketOverload)) {
            if (!arrayBracketOverload) {
                arrayBracketOverload = child
            } else {
                SyntaxErrorException("Array bracket overload already declared for class '#location'", child)
            }
        } else {
            SyntaxMessage.error("Statement '#child' used in wrong context", child)
        }
        
        super.addChild(child)
    }
    
    public replace(Node toReplace, Node replacement) -> Bool =>
        toReplace.class.isOfType(FieldDeclaration) &&
            fields.replace((FieldDeclaration)toReplace, (FieldDeclaration)replacement) ||
        toReplace.class.isOfType(FunctionDeclaration) &&
            functions.replace((FunctionDeclaration)toReplace, (FunctionDeclaration)replacement)
    
    public findCompatibleFunctions(String name, Value[] argumentTypes = null) =>
        functions.filter(func => {
            func.name.equals(name) &&
            (!argumentTypes || func.compatibleArguments(argumentTypes))
        })
    
    public findFunction(String name, Bool searchAncestors = true) -> FunctionDeclaration => null {
        if (var function = functions.firstWhere({ _.name.equals(name) })) {
            return function
        }
        
        if (searchAncestors) {
            if (function = extendedClass?.getClass(parentFile)?.findFunction(name)) {
                return function
            } else if (function = implementedInterfaces.firstNonNull({ _.getClass(parentFile)?.findFunction(name) })) {
                return function
            }
        }
    }
    
    public generateTemporaryScopeNode() -> Node {
        return new BodyFunction(this)
    }
    
    public cloneTo(ClassDeclaration other) => other {
        super.cloneTo(other)
        GenericCompatible.super.cloneTo(other)
        Abstractable.super.cloneTo(other)
        
        other.extendedClass = extendedClass
        other.implementedInterfaces = implementedInterfaces
        other.fields = fields
        other.functions = functions
        other.arrayBracketOverload = arrayBracketOverload
    }
}