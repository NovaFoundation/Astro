package "novex/astro/tree/nodes/functions"

import "novex/astro/SyntaxMessage"
import "novex/astro/InvalidParseException"
import "novex/astro/tree/SyntaxTree"
import "novex/astro/tree/nodes/Node"
import "novex/astro/tree/nodes/Type"
import "novex/astro/tree/nodes/variables/InstanceDeclaration"
import "novex/astro/tree/nodes/variables/VariableDeclaration"
import "novex/astro/tree/nodes/generics/GenericParameter"
import "novex/astro/tree/nodes/generics/GenericCompatible"
import "novex/astro/util/Location"

import "novex/astro/util/CompilerStringFunctions"

class FunctionDeclaration extends InstanceDeclaration implements CallableFunction, GenericCompatible {
    visible FunctionDeclaration parentFunction => this
    
    visible Parameter objectReference
    
    /// Create a new FunctionDeclaration node.
    /// 
    /// #exampleInputs
    /// 
    /// parent:   The parent of the FunctionDeclaration node.
    /// location: The location of the input text in the source file.
    public construct(Node parent = null, Location location = Location.INVALID) {
        super(parent, location)
        
        parameterList = new ParameterList(this)
        genericParameters = new GenericParameter[0]
    }
    
    /// Parse the given input into a FunctionDeclaration node. If the input cannot be parsed
    /// into a FunctionDeclaration node, null is returned.
    /// 
    /// #exampleInputs:
    ///     Example inputs:
    ///      *
    ///      *
    ///      *
    /// 
    /// input:    The input string to parse into a FunctionDeclaration instance.
    /// parent:   The parent of the FunctionDeclaration node.
    /// location: The location of the input text in the source file.
    /// require:  Whether or not to throw a syntax error if the input could not
    ///           be parsed.
    /// return:   The parsed FunctionDeclaration node, if successful; null otherwise.
    public static parse(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) -> FunctionDeclaration => null {
        if ((let signature  = findSignature(input)) &&
            (let parameters = findParameters(input)) &&
            (let returnType = findReturnType(input))) {
            var node = new FunctionDeclaration(parent, location)
            
            if ((let genericParams = findGenericParameters(input)) && !node.parseGenericParameters(genericParams, require)) {
                toss new InvalidParseException("Could not parse method generic parameters '#genericParams'", node)
            } else if (node.parseSignature(signature, require) &&
                node.parseParameters(parameters, require) &&
                node.parseType(returnType)) {
                if (!node.isStatic) {
                    node.objectReference = new Parameter(node.parameterList)
                    node.objectReference.name = "this"
                    node.objectReference.type = node.parentClass.type
                }
                if (node.name == "zip") {
                    Console.log("!!!!!!!!!!!!#node.genericParameters")
                }
                return node
            }
        }
        
        if (require) {
            SyntaxMessage.error("Invalid function declaration '#input'", parent)
        }
    }
    
    static findSignature(String input) =>
        (let paramStart = input.indexOf('(')) > 0 &&
        (let genericStart = input.indexOf('<', defaultReturnValue: input.count)) > 0 ?
            input.substring(end: Math.min(paramStart, genericStart)).trim() :
            null
    
    static findGenericParameters(String input) =>
        (let start = input.findCharOnTopLevel('<')) >= 0 &&
        (let end = input.findEndingMatch(start, '<', '>')) > 0 ?
            input.substring(start, end + 1).trim() :
            null
    
    static findReturnType(String input) -> String {
        let start = input.indexOf('(')
        
        if (start < 0) {
            return null
        }
        
        var end = input.findEndingMatch(start, '(', ')')
        
        if (end < 0) {
            return null
        }
        
        if (input.containsAllWhitespaceAfter(end + 1)) {
            return ""
        }
        
        end = input.nextWordIndex("->", end + 1)
        
        if (end < 0) {
            return null
        }
        
        return input.substring(start: end + 2).trim()
    }
    
    static findParameters(String input) =>
        (let start = input.indexOf('(')) >= 0 &&
        (let end = input.findEndingMatch(start, '(', ')')) >= 0 ?
            input.substring(start + 1, end).trim() :
            null
    
    public parseSignature(String signature, Bool require) =>
        signature.splitValues().filterEmpty().all((word, index, words) => {
            index == words.count - 1 ?
                parseName(word) :
                parseModifier(word)
        }, false)
    
    public parseType(String input) =>
        input.count == 0 || super.parseType(input)
        
    [Override]
    public writeAnnotationSeparator() => "\n"
    
    public writeReturnType() => type ? " -> #type" : ""
    
    public writeHeader() =>
        "#name".replace(/\s+/, " ").trim() +
        writeGenericParameters() +
        "#parameterList.toNova()" +
        writeReturnType()
    
    public writeNova() => writeHeader()
    
    public cloneTo(FunctionDeclaration other) => other {
        super.cloneTo(other)
        GenericCompatible.super.cloneTo(other)
        
        other.parameterList = parameterList
        other.objectReference = objectReference
    }
}