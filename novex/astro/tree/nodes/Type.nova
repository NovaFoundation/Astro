package "novex/astro/tree/nodes"

import "novex/astro/SyntaxMessage"
import "novex/astro/InvalidParseException"
import "novex/astro/util/Location"
import "novex/astro/tree/nodes/generics/GenericType"
import "novex/astro/tree/nodes/generics/GenericArgument"
import "novex/astro/tree/nodes/generics/GenericParameter"

import "novex/astro/util/CompilerStringFunctions"

[AutoFinal]
class Type {
    visible Bool isGeneric => false
    visible Bool isPrimitiveType => PRIMITIVE_TYPES.contains(name)
    
    visible static String[] PRIMITIVE_TYPES = ["Bool", "Byte", "Short", "Int", "Long", "Float", "Double", "Char"]
    
    visible var GenericArgument[] genericArguments = new GenericArgument[0]
    
    public var String name
    
    public getClass(NovaFile file) =>
        file.imports.firstWhere({ _.className == name })?.classDeclaration
    
    public construct(public var String name = null) {}
    
    public static parse(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) -> Type => null {
        if (let array = ArrayType.parse(input, parent, location, require)) {
            return array
        }
        
        var node = new Type(input)
        
        if (!node.parseGenericArguments(input, parent, location)) {
            toss new InvalidParseException("Unable to parse generic arguments for type '#input'", parent)
        } else if (!node.name.isIdentifier) {
            toss new InvalidParseException("Invalid type '#input'", parent)
        } else {
            if (parent) {
                var param = node.getGenericParameter(parent)
                
                if (param) {
                    node = new GenericType(node, param)
                }
            }
            
            return node
        }
    }
    
    getGenericParameter(Node parent) -> GenericParameter => null {
        if (parent.parentFunction) {
            let param = parent.parentFunction.genericParameters.firstWhere({ _.type.name.equals(name) })
            
            if (param) {
                return param
            }
        }
        if (parent.parentClass) {
            let param = parent.parentClass.genericParameters.firstWhere({ _.type.name.equals(name) })
            
            if (param) {
                return param
            }
        }
    }
    
    parseGenericArguments(String input, Node parent, Location location) => true {
        let genericIndex = input.indexOf('<')
        
        if (genericIndex > 0) {
            name = input.substring(end: genericIndex)
            
            let end = input.findEndingMatch(genericIndex, '<', '>')
            
            if (end > 0) {
                genericArguments = input.substring(genericIndex + 1, end).splitAtCommas().map({ new GenericArgument(parent, location, _) })
                
                if (genericArguments.any({ !_.type })) { // if any generic arguments didnt parse
                    return false
                }
            }
        }
    }
    
    public isValid(Node parent) =>
        isPrimitiveType ||
        parent.parentFile.imports.any({ _.className.equals(name) })
    
    public writeGenericArguments() =>
        genericArguments.count > 0 ?
            "<#{genericArguments.join(", ")}>" :
            ""
    
    public toString() => writeNova()
    public toNova() => writeNova()
    public writeNova() => "#name" + writeGenericArguments()
    
    public cloneTo(Type other) => other {
        other.name = name
        other.genericArguments = genericArguments
    }
}