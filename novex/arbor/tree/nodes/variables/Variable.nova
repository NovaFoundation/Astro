package "novex/arbor/tree/nodes/variables"

import "novex/arbor/InvalidParseException"
import "novex/arbor/util/Location"
import "novex/arbor/tree/nodes/Node"
import "novex/arbor/tree/nodes/Type"
import "novex/arbor/tree/nodes/Identifier"
import "novex/arbor/tree/nodes/Accessible"
import "novex/arbor/tree/nodes/Value"
import "novex/arbor/tree/nodes/operations/Assignable"

import "novex/arbor/util/CompilerStringFunctions"

class Variable extends Identifier implements Accessible, Assignable {
    public VariableDeclaration declaration
    
    public Type type => declaration.type
    
    /// Create a new Variable node.
    /// 
    /// parent:   The parent of the Variable node.
    /// location: The location of the input text in the source file.
    public construct(Node parent = null, Location location = Location.INVALID, VariableDeclaration declaration = null) {
        super(parent, location)
        
        this.declaration = declaration
    }
    
    public static findVariable(String name, Node parent, Location location = parent.location) -> Variable => null {
        if (name.isIdentifier) {
            var declaration = parent.findVariableDeclaration(name)
            
            if (declaration) {
                return new Variable(parent, location, declaration)
            } else {
                toss new InvalidParseException("Could not find declaration for identifier '#name'")
            }
        }
    }
    
    [Override]
    public onAssigned(Value value) {
        ((Assignable)declaration).onAssigned(value)
    }
    
    public cloneTo(Variable other) => other {
        super.cloneTo(other)
        Accessible.super.cloneTo(other)
        
        other.declaration = declaration
    }
    
    public writeNova() => declaration.name + writeArrayAccess() + writeAccessedNodes()
}